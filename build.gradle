plugins {
  id "com.gradle.plugin-publish" version "0.9.0"
  id "com.github.hierynomus.license" version "0.11.0"
  id 'nebula.nebula-release' version '2.2.5'
}

apply plugin: 'groovy'

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.xebialabs.deployit.engine:remote-booter:2015.1"
}

release {
  defaultVersionStrategy = nebula.plugin.release.NetflixOssStrategies.SNAPSHOT
}
tasks.release.dependsOn tasks.publishPlugins
tasks.releaseCheck.dependsOn tasks.clean

clean.doLast {
  project.delete 'src/test/resources/HelloDeployment/build'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

pluginBundle {
  website = project_url
  vcsUrl = project_scm
  description = project_description_full
  tags = ['deployment', 'deploy', 'xldeploy', 'cd', 'continuous delivery']

  plugins {
    xlDeployPlugin {
      id = 'com.xebialabs.xl-deploy'
      displayName = 'Gradle XL Deploy plugin'
    }
  }

  mavenCoordinates {
    groupId = project_group_id
    artifactId = project_artifact_id
  }
}

license {
  header rootProject.file('src/main/license/xebialabs_community.license')
  strictCheck true
  exclude "**/*.txt"
  excludes(["src/test/resources/HelloDeployment/.gradle/**", "src/test/resources/HelloDeployment/userHome/**"])
}
// workaround for a bug in license plugin
tasks.licenseTest.doFirst {
  delete 'src/test/resources/HelloDeployment/userHome/caches/modules-2/'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "http://www.knopflerfish.org/maven2/"
  }
  maven {
    url "https://dist.xebialabs.com/public/maven2/"
  }
}
