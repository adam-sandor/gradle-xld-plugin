apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.hierynomus.license'

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.xebialabs.deployit.engine:remote-booter:2015.1"
}

clean.doLast {
  project.delete 'src/test/resources/HelloDeployment/build'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

def pomConfig = {
  name 'XL Deploy Gradle Plugin'
  url project_url
  scm { url project_scm }

  organization {
    name 'XebiaLabs'
    url 'http://www.xebialabs.com'
  }
}

publishing {
  publications {
    gradlePlugin(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      groupId project_group_id
      artifactId project_artifact_id

      pom.withXml {
        asNode().appendNode('description', project_description)
        asNode().children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = bintrayUsername
  key = bintrayApiKey

  publications = ['gradlePlugin']
  dryRun = false
  publish = false
  pkg {
    repo = 'maven'
    userOrg = 'xebialabs'
    name = project_artifact_id
    desc = project_description
    websiteUrl = project_url
    issueTrackerUrl = project_issues_url
    vcsUrl = project_scm
    licenses = ['Apache-2.0']
    labels = ['deployment', 'deploy', 'xldeploy', 'cd', 'continuous delivery', 'gradle']
    publicDownloadNumbers = true
    version {
      attributes = ['gradle-plugin': "com.xebialabs.xl-deploy:$project_group_id:$project_artifact_id"]
      vcsTag = "$project_name-${project.version}"
    }
  }
}

license {
  header rootProject.file('src/main/license/xebialabs_community.license')
  strictCheck true
  exclude "**/*.txt"
  excludes(["src/test/resources/HelloDeployment/.gradle/**", "src/test/resources/HelloDeployment/userHome/**"])
}
// workaround for a bug in license plugin
tasks.licenseTest.doFirst {
  delete 'src/test/resources/HelloDeployment/userHome/caches/modules-2/'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "http://www.knopflerfish.org/maven2/"
  }
  maven {
    url "https://dist.xebialabs.com/public/maven2/"
  }
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
  }
}
